#!/usr/bin/env perl

# Author Nigel Horne: njh@bandsman.co.uk
# Copyright (C) 2015, Nigel Horne

# Usage is subject to licence terms.
# The licence terms of this software are as follows:
# Personal single user, single computer use: GPL2
# All other users (including Commercial, Charity, Educational, Government)
#	must apply in writing for a licence for use from Nigel Horne at the
#	above e-mail.

# FIXME: -a Goes through the entire file once for each day without remembering
#	anything
# TODO: add option to print in generations order rather than date order.

# -a: all days otherwise just today
# -b: only print birthdays
# -c: Give citations on detailed listing
# -d: give a detailed lifetime information about the person
# -D: only print anniversaries of deaths
# -f: treat warnings as fatals, implies -w
# -l: include living people
# -p: print the biography of the given person
# -v: verbose - for debugging
# -w: print warning about unlikely data - a sort of lint for Gedcom files

use strict;
use warnings;
# use diagnostics;
# use warnings::unused;

use Gedcom;
use Genealogy::Gedcom::Date;
use Date::Parse;	# For strptime
use Getopt::Std;
use Geo::Coder::OSM;
use Term::ANSIColor;
use Lingua::EN::NameCase;
use Lingua::EN::NameParse;
use Lingua::EN::Numbers::Ordinate;
use DateTime::Duration;
use Tie::Static;
no lib '.';

my %opts;
getopts('abcdDfh:lp:wv', \%opts);
die "Usage: $0 [ -a ] [ -b ] [ -c ] [ -d ] [ -D ] [ -f ] [ -h home-person-name ] [ -l ] [ -p person-to-print ] [ -w ] filename\n" unless($ARGV[0]);

my $me;
my @myancestors;
my %warned;
my %printed;
my %places;

my $ged = Gedcom->new(gedcom_file => $ARGV[0], read_only => 1);

if($opts{h}) {
	$me = $ged->get_individual($opts{h});
	unless($me) {
		die "$0: Can't find '$opts{h}' in $ARGV[0]";
	}
}

if($opts{'f'}) {
	$opts{'w'} = 1;
}

if($opts{'w'}) {
	die '-w option needs -d option' unless($opts{'d'});

	# Cache calls to openstreetmap
	require File::Spec;
	File::Spec->import();

	require HTTP::Cache::Transparent;
	HTTP::Cache::Transparent->import();

	my $cachedir = File::Spec->catfile(File::Spec->tmpdir(), 'cache', 'http-cache-transparent');

	HTTP::Cache::Transparent::init({
		BasePath => $cachedir,
		# Verbose => 1,
		NoUpdate => 60 * 60 * 24,
		MaxAge => 30 * 24
	}) || die "$0: $cachedir: $!";
}

my $date_parser = Genealogy::Gedcom::Date->new();
my $geocoder = Geo::Coder::OSM->new();
my $nameparser = Lingua::EN::NameParse->new(extended_titles => 1);
my $ninemonths = DateTime::Duration->new(months => 9);

my $year = (localtime)[5];
$year += 1900 if($year < 1900);

my @everyone;
unless($opts{'p'}) {
	@everyone = $ged->individuals();
}

if($opts{'c'} && !$opts{'d'}) {
	die '-c only makes sense with the -d option';
}

if($opts{a}) {
	die '-b and -a should not be given together' if($opts{'b'});
	die '-D and -a should not be given together' if($opts{'D'});

	foreach my $month(0..11) {
		foreach my $day(1..29) {
			generate(day => $day, month => $month, everyone => \@everyone);
		}
		if($month == 1) {
			next;
		}
		generate(day => 30, month => $month, everyone => \@everyone);
		if(($month == 0) || ($month == 2) || ($month == 4) || ($month == 6) || ($month == 7) || ($month == 9) || ($month == 11)) {
			generate(day => 31, month => $month, everyone => \@everyone);
		}
	}
	generate(day => -1, month => -1, everyone => \@everyone);
} elsif($opts{'b'}) {
	die '-b and -d should not be given together' if($opts{'d'});
	die '-b and -a should not be given together' if($opts{'a'});
	my($day, $month) = (localtime)[3,4];
	if($opts{'D'}) {
		print "Today's birthdays:\n";
	}
	generate(day => $day, month => $month, onlybirthdays => 1, onlydeaths => 0, everyone => \@everyone);
	if($opts{'D'}) {
		print "Today's anniversaries of deaths:\n";
		generate(day => $day, month => $month, onlybirthdays => 0, onlydeaths => 1, everyone => \@everyone);
	}
} elsif($opts{'D'}) {
	die '-D and -d should not be given together' if($opts{'d'});
	die '-D and -a should not be given together' if($opts{'a'});
	my($day, $month) = (localtime)[3,4];
	generate(day => $day, month => $month, onlybirthdays => 0, onlydeaths => 1, everyone => \@everyone);
} elsif($opts{'p'}) {
	die '-p and -D should not be given together' if($opts{'D'});
	die '-p and -b should not be given together' if($opts{'b'});
	die '-p and -a should not be given together' if($opts{'a'});

	my $person = $ged->get_individual($opts{p});
	if($person) {
		my($day, $month) = (localtime)[3,4];
		print_person(person => $person, day => $day, month => $month);
	} else {
		die "$0: Can't find '$opts{p}' in $ARGV[0]";
	}
} else {
	my($day, $month) = (localtime)[3,4];
	generate(day => $day, month => $month, everyone => \@everyone);
}

sub generate {
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	foreach my $person(@{$params{'everyone'}}) {
	# foreach my $person(sort { $a->get_value('last name') cmp $b->get_value('last name') } $ged->individuals()) {
		next if($me && ($person eq $me));

		if(!$printed{$person->{'xref'}}) {
			$params{'person'} = $person;
			print_person(\%params);
		}
	}
}

sub print_person
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	# Gather the information on this person
	my $person = $params{'person'};
	my $dob = get_value({ person => $person, value => 'birth date' });

	my $onlybirthdays = $params{'onlybirthdays'};
	my $onlydeaths = $params{'onlydeaths'};

	die "BUG: onlydeaths and onlybirths given" if($onlydeaths && $onlybirthdays);

	return if($onlybirthdays && !defined($dob));

	my $dod = get_value({ person => $person, value => 'death date' });

	return if($onlydeaths && !defined($dod));

	my $yod;	# Year of death
	if($dod && ($dod =~ /.*?(\d{3,4})/)) {
		$yod = $1;
	}

	my $yob;	# Year of birth
	if($dob && ($dob =~ /.*?(\d{3,4})/)) {
		$yob = $1;
	} elsif($dob) {
		if($opts{'w'} && !$warned{$person}) {
			if($opts{'f'}) {
				die $person->name(), ": invalid date of birth $dob\n";
			}
			warn $person->name(), ": invalid date of birth $dob\n";
			$warned{$person} = 1;
		}
	} else {
		$yob = $dob;
	}

	if((!defined($opts{l})) && (!defined($yod)) && defined($yob) && ($yob > 1900)) {
		# Assuming living if we don't have a date of death and
		# they were born after 1900
		return;
	}

	unless($dob || $dod || $opts{'a'} || $opts{'p'}) {
		return;
	}

	print 'Checking ', $person->as_string({ include_years => 1 }), "\n" if($opts{'v'});

	my $print = 0;
	my $aob;
	my $aod;
	my $day = $params{'day'};
	my $month = $params{'month'};

	if($dob && ($dob !~ /^\d{4}$/)) {
		my @btime = strptime("$dob 12:00");
		if(defined($btime[3]) && ($btime[3] == $day) && ($btime[4] == $month) && $btime[5]) {
			$print = 1;
			if(!$opts{a}) {
				$aob = (localtime)[5] - $btime[5];
				if($aob <= 0) {
					$aob += 1900;
				}
			}
		}
	}
	return if($onlybirthdays && !defined($aob));

	if($dod && ($dod !~ /^\d{4}$/)) {
		my @dtime = strptime("$dod 12:00");
		if(defined($dtime[3]) && ($dtime[3] == $day) && ($dtime[4] == $month) && $dtime[5]) {
			$print = 1;
			if(!$opts{a}) {
				$aod = (localtime)[5] - $dtime[5];
				if($aod <= 0) {
					$aod += 1900;
				}
			}
		}
	}
	return if($onlydeaths && !defined($aod));

	return unless($print || ($day == -1) || $opts{'p'});

	my @siblings = $person->siblings();
	my $mother = $person->mother();
	my $father = $person->father();
	my @spouses = $person->spouse();
	my $family = $person->fams();

	if($opts{'w'} && (!$family) && (scalar(@siblings) == 0) && (!$mother) && (!$father) && (scalar(@spouses) == 0)) {
		if($opts{'f'}) {
			die $person->as_string() . ': not connected to the tree';
		}
		red_warning({ person => $person, warning => 'not connected to the tree' });
	}

	print $person->as_string({
		include_years => 1,
		nee => 1,
		middle_names => 1,
		title => 1,
	}), "\n";

	my $name = $person->name();
	$name =~ s/\///g;
	$nameparser->parse($name);
	my %name_components = $nameparser->components();
	my $firstname = $name_components{'given_name_1'};
	my $lastname = $name_components{'surname_1'};

	my ($birth_dt, $death_dt);
	my $birth = $person->get_record('birth');
	my $dateofbirth = get_value({ person => $person, value => 'birth date' });
	if((!$dateofbirth) && $birth) {
		$dateofbirth = $birth->date();
	}

	my $death = $person->get_record('death');
	my $dateofdeath = get_value({ person => $person, value => 'death date' });
	if((!$dateofdeath) && $death) {
		$dateofdeath = $death->date();
	}

	my %citations;
	my $citationcount = 0;
	my $birthcitation;
	my $deathcitation;
	if($opts{'c'}) {
		if($birth) {
			my $src = $birth->source();
			if($src) {
				$src = $ged->get_source($src) unless ref($src);
				$birthcitation = ++$citationcount;
				$citations{$birthcitation} = $src;
			}
		}
		if($death) {
			my $src = $death->source();
			if($src) {
				$src = $ged->get_source($src) unless ref($src);
				if($birthcitation && ($citations{$birthcitation} eq $src)) {
					$deathcitation = $birthcitation;
				} else {
					$deathcitation = ++$citationcount;
					$citations{$deathcitation} = $src;
				}
			}
		}
	}


	if($dateofbirth && ($dateofbirth !~ /^\d{4}$/) && ($dateofbirth =~ /^\d/)) {
		my $d;
		eval {
			$d = $date_parser->parse_date_value(date => $dateofbirth);
		};
		if($d) {
			$birth_dt = $d->{one_date};
		}
	}
	if($dateofdeath && ($dateofdeath !~ /^\d{4}$/) && ($dateofdeath =~ /^\d/)) {
		my $d;
		eval {
			$d = $date_parser->parse_date_value(date => $dateofdeath);
		};
		if($d) {
			$death_dt = $d->{one_date};
		}
	}
	if($opts{'d'}) {
		print "\t";

		my $placeofbirth = get_value({ person => $person, value => 'birth place' });
		if((!$placeofbirth) && $birth) {
			$placeofbirth = $birth->place();
		}

		my $baptism = $person->get_record('baptism');
		my $dateofbaptism = get_value({ person => $person, value => 'baptism date' });
		if((!$dateofbaptism) && $baptism) {
			$dateofbaptism = $baptism->date();
		}
		my $placeofbaptism = get_value({ person => $person, value => 'baptism place' });
		if((!$placeofbaptism) && $baptism) {
			$placeofbaptism = $baptism->place();
		}

		my $marriage = $person->get_record('marriage') || $person->get_record('fams marriage');
		if((!defined($marriage)) && scalar(@spouses)) {
			$marriage = $spouses[0]->get_record('marriage');
		}
		my $dateofmarriage = get_value({ person => $person, value => 'marriage date' });
		if((!$dateofmarriage) && $marriage) {
			$dateofmarriage = $marriage->date();
		}
		my $placeofmarriage = get_value({ person => $person, value => 'marriage place' });
		if((!$placeofmarriage) && $marriage) {
			$placeofmarriage = $marriage->place();
		}
		my $marriagecitation;
		if($opts{'c'}) {
			if($marriage) {
				my $src = $marriage->source();
				if($src) {
					$src = $ged->get_source($src) unless ref($src);
					if($birthcitation && ($citations{$birthcitation} eq $src)) {
						$marriagecitation = $birthcitation;
					} elsif($deathcitation && ($citations{$deathcitation} eq $src)) {
						$marriagecitation = $deathcitation;
					} else {
						$marriagecitation = ++$citationcount;
						$citations{$marriagecitation} = $src;
					}
				}
			}
		}

		my $placeofdeath = get_value({ person => $person, value => 'death place' });
		if((!$placeofdeath) && $death) {
			$placeofdeath = $death->place();
		}

		my $burial = $person->get_record('burial');
		my $dateofburial = get_value({ person => $person, value => 'burial date' });
		if((!$dateofburial) && $burial) {
			$dateofburial = $burial->date();
		}
		my $placeofburial = get_value({ person => $person, value => 'burial place' });
		if((!$placeofburial) && $burial) {
			$placeofburial = $burial->place();
		}

		my $pronoun = $person->pronoun();
		my $sex = $person->sex();
		my @occupations = $person->get_value('occupation');
		my @residences = $person->residence();

		my @children;
		my $numberofchildren = 0;
		if($family) {
			$numberofchildren = $family->number_of_children();
			@children = $family->children();
		}

		my $relationship;
		my $spouserelationship;
		my $spouse;
		my %places_printed;

		if($dateofbirth && $opts{'w'} && scalar(@siblings)) {
			foreach my $sibling(@siblings) {
				my $siblingbirth = get_value({ person => $sibling, value => 'birth date' });
				if($siblingbirth && ($siblingbirth eq $dateofbirth) && ($sibling->name() eq $person->name())) {
					if($opts{'f'}) {
						die $person->name(), ': Possible duplicate person';
					}
					red_warning({ person => $sibling, warning => 'possible duplicate person' });
				}
			}
		}

		if($me) {
			$relationship = $me->relationship($person);
			if((!$relationship) && scalar(@spouses)) {
				foreach my $s(@spouses) {
					$spouserelationship = $me->relationship($s);
					if($spouserelationship) {
						$spouse = $s;
						last;
					}
				}
			}
		}

		# TODO: print all occupations
		if($occupations[0]) {
			print 'A';
			if($occupations[0] =~ /^[aeiou]/i) {
				print 'n';
			}
			print ' ', lc($occupations[0]);
		}
		my $print_sibling_count = 0;
		if($father || $mother) {
			if(scalar(@occupations)) {
				print ' and the ';
			} else {
				print 'The ';
			}

			if($birth_dt) {
				if(scalar(@siblings)) {
					my $count = 1;
					$print_sibling_count = 1;
					foreach my $sibling(@siblings) {
						my $dob = get_value({ person => $sibling, value => 'birth date' });
						if($dob) {
							my $d;
							eval {
								$d = $date_parser->parse_date_value(date => $dob);
							};
							if($d) {
								$d = $d->{one_date};
								$count++ if($d && ($d < $birth_dt));
							} else {
								$print_sibling_count = 0;
								last;
							}
						}
					}
					if($print_sibling_count) {
						if((scalar(@siblings)) && ($count == (scalar(@siblings) + 1))) {
							print 'last';
						} else {
							print ordinate($count);
						}
						print ' of ', scalar(@siblings) + 1, ' children';
					} else {
						print 'child';
					}
				} else {
					# print ' only child of';
					print 'child';
				}
			} else {
				print 'child';
			}

			print ' of';

			if($father) {
				print ' ', $father->as_string();
				my @occupations = $father->get_value('occupation');
				# TODO: print all occupations
				if($occupations[0]) {
					print ' (a';
					if($occupations[0] =~ /^[aeiou]/i) {
						print 'n';
					}
					print ' ', lc($occupations[0]), ')';
				}
				if($mother) {
					print ' and';
				}
			}
			if($mother) {
				print ' ', $mother->as_string({ give_maidenname => 0 });
				my @occupations = $mother->get_value('occupation');
				# TODO: print all occupations
				if($occupations[0]) {
					print ' (a';
					if($occupations[0] =~ /^[aeiou]/i) {
						print 'n';
					}
					print ' ', lc($occupations[0]), ')';
				}
			}
			print ', ' unless($relationship);
		}

		my $haveprintedspousename;
		if($relationship) {
			if($father || $mother || $occupations[0]) {
				print ', y';
			} else {
				print 'Y';
			}
			print "our $relationship ";
		} elsif($spouserelationship) {
			if($sex eq 'F') {
				print 'The wife';
			} else {
				print 'The husband';
			}
			print " of your $spouserelationship ",
				$spouse->as_string({ middle_names => 1 }),
				', ';
			$haveprintedspousename = 1;
		}

		if($firstname) {
			print $firstname;
		} else {
			print $pronoun;
		}

		my $end_of_sentence = 1;
		if($aob && $aod) {
			print " was born $aob years and died $aod years ago today";
			print "[$birthcitation]" if($birthcitation);
			print "[$deathcitation]" if($deathcitation);
			print '.';
		} elsif($aob) {
			print " was born $aob ", ($aob == 1) ? 'year' : 'years', ' ago today';
			if($placeofbirth) {
				my $p = place({ person => $person, place => $placeofbirth });
				print $p;
				$places_printed{$p} = 1;
			}
			print "[$birthcitation]" if($birthcitation);
			print '.';
		} elsif($aod) {
			print " died $aod ", ($aod == 1) ? 'year' : 'years', ' ago today';
			if($placeofdeath) {
				my $p = place({ person => $person, place => $placeofdeath });
				print $p;
				$places_printed{$p} = 1;
			}
			print "[$deathcitation]" if($deathcitation);
			print '.';
		} else {
			$end_of_sentence = 0;
		}

		if((!$aob) && ($dateofbirth || $dateofbaptism || $placeofbirth || $placeofbaptism)) {
			print "  $pronoun" if($aob || $aod);
			if($dateofbirth || $placeofbirth) {
				print ',' if($print_sibling_count && (!$opts{'a'}) && (!$aob) && (!$aod) && !$opts{'p'});

				print ' was born';
				if($placeofbirth) {
					if($aod && $placeofdeath && ($placeofbirth eq $placeofdeath)) {
						print ' there';
					} else {
						print place({ person => $person, place => $placeofbirth });
					}
				}
				if($dateofbirth) {
					print ' ', year({
						person => $person,
						date => $dateofbirth,
						must_predate => $death_dt
					});
				}
				print "[$birthcitation]" if($birthcitation);
				if($dateofbaptism || $placeofbaptism) {
					if(scalar(@spouses)) {
						print ', was';
					} else {
						print ' and';
					}
				}
				$end_of_sentence = 0;
			}
			if($dateofbaptism || $placeofbaptism) {
				print ' ';
				unless($dateofbirth || $placeofbirth) {
					print 'was ';
				}
				if($ENV{'LANG'} =~ /^en_US/) {
					print 'baptized';
				} else {
					print 'baptised';
				}
				if($placeofbaptism) {
					if($aod && $placeofbirth && ($placeofbaptism eq $placeofbirth)) {
						print ' there';
					} else {
						my $p = place({ person => $person, place => $placeofbaptism });
						print $p;
						$places_printed{$p} = 1;
					}
				}
				if($dateofbaptism) {
					print ' ', year({
						person => $person,
						date => $dateofbaptism,
						must_postdate => $birth_dt,
						must_predate => $death_dt
					});
					print_sibling_baptism({
						person => $person,
						siblings => \@siblings,
						date => $dateofbaptism,
						birthdate => $dateofbirth,
					});
				}
				$end_of_sentence = 0;
			}
			if(scalar(@spouses)) {
				if($aod || $aob) {
					print ' and';
				} else {
					print '.';
					$end_of_sentence = 1;
				}
			} else {
				print '.';
				$end_of_sentence = 1;
			}
		} elsif($aob && ($dateofbaptism || $placeofbaptism)) {
			print "  $pronoun was ";

			if($ENV{'LANG'} =~ /^en_US/) {
				print 'baptized';
			} else {
				print 'baptised';
			}
			if($placeofbaptism) {
				if($placeofbirth && ($placeofbaptism eq $placeofbirth)) {
					if($aod) {
						print ' in ',
							lcfirst($person->possessive()),
							' home town';
					} else {
						print ' there';
					}
				} else {
					my $p = place({ person => $person, place => $placeofbaptism });
					print $p;
					$places_printed{$p} = 1;
				}
			}
			if($dateofbaptism) {
				print ' ', year({
					person => $person,
					date => $dateofbaptism,
					must_postdate => $birth_dt,
					must_predate => $death_dt
				});
				print_sibling_baptism({
					person => $person,
					siblings => \@siblings,
					date => $dateofbaptism,
					birthdate => $dateofbirth,
				});
			}
			if(scalar(@spouses)) {
				print ' and';
				$end_of_sentence = 0;
			} else {
				print '.';
				$end_of_sentence = 1;
			}
		}

		my $all_children_are_alive = 1;
		if($yob && ($year >= $yob + 150)) {
			$all_children_are_alive = 0;
		} else {
			foreach my $child(@children) {
				if($child->get_record('death') || get_value({ person => $child, value => 'death date' })) {
					$all_children_are_alive = 0;
					last;
				}
				if($child->get_record('burial')) {
					$all_children_are_alive = 0;
					last;
				}
				my $dob = get_value({ person => $child, value => 'birth date' });
				if($dob) {
					if(($dob =~ /^\d{4}$/) && ($dob < ($year - 100))) {
						$all_children_are_alive = 0;
						last;
					}
					if($dob =~ /^\d/) {
						my $d;
						eval {
							$d = $date_parser->parse_date_value(date => $dob);
						};
						if($d && ($d->{one_date}->strftime('%Y') < ($year - 120))) {
							$all_children_are_alive = 0;
							last;
						}
					}
				}
			}
		}
		if(scalar(@spouses) &&
		   ((!$spouserelationship) || $placeofmarriage || $dateofmarriage)) {
			print "  $pronoun" if($end_of_sentence);

			if(scalar(@spouses) == 1) {
				if($placeofmarriage || $dateofmarriage) {
					print ' married ';
				} else {
					print ' was married to ';
				}
				if($haveprintedspousename) {
					print $spouses[0]->given_names();
				} else {
					print $spouses[0]->as_string();
					$haveprintedspousename = 1;
				}
				if($numberofchildren) {
					print ' with whom ', lcfirst($pronoun);
					if((!$all_children_are_alive) || (!$opts{'l'}) || ($yob && ($year > $yob + 100))) {
						print ' had ';
					} elsif((!$dateofdeath) && (!$dateofburial) && ($spouses[0]) && !$spouses[0]->death()) {
						print ' has had ';
					} else {
						print $dateofdeath || $dateofburial ? ' had ' : ' has ';
					}
					if($numberofchildren == 1) {
						if($all_children_are_alive) {
							print '1 surviving child, ';
						} else {
							print '1 child, ';
						}
					} else {
						if($all_children_are_alive) {
							print "$numberofchildren surviving children, ";
						} else {
							print "$numberofchildren children, ";
						}
					}
					if($numberofchildren == 1) {
						print $children[0]->given_names();
					} elsif($numberofchildren > 1) {
						my $childnames = join(', ', map { $_->given_names() } @children);
						substr($childnames, rindex($childnames, ', '), 2, ' and ');
						print $childnames;
					}
				}
				print ',' if(($dateofmarriage || $placeofmarriage) && $numberofchildren);
				if($placeofmarriage && $dateofmarriage &&
				   (($placeofbaptism and ($placeofmarriage eq $placeofbaptism)) ||
				    ($aod && $placeofbirth && ($placeofmarriage eq $placeofbirth)))) {
					print ' there ',
						year(person => $person, string => $dateofmarriage, must_postdate => $birth_dt, must_predate => $death_dt);
				} else {
					print ' ', year(person => $person, string => $dateofmarriage, must_postdate => $birth_dt, must_predate => $death_dt) if($dateofmarriage);
					if($placeofmarriage) {
						my $p = place({ place => $placeofmarriage, person => $person, places_printed => \%places_printed });
						print $p;
						$places_printed{$p} = 1;
					}
				}
				print "[$marriagecitation]" if($marriagecitation);
			} else {
				print ' was married ';
				if(scalar(@spouses) == 2) {
					print 'twice';
				} else {
					print scalar(@spouses), ' times';
				}
				my $names = join(', ', map { $_->as_string() } @spouses);
				substr($names, rindex($names, ', '), 2, ' and ');
				print ", to $names";
				# TODO: print the date and places of the marriages
				if($numberofchildren) {
					print ".  $pronoun ",
						$all_children_are_alive ? 'has ' : 'had ';
					if($numberofchildren == 1) {
						if($all_children_are_alive) {
							print '1 surviving child, ';
						} else {
							print '1 child, ';
						}
						print $children[0]->given_names();
					} else {
						if($all_children_are_alive) {
							print "$numberofchildren surviving children, ";
						} else {
							print "$numberofchildren children, ";
						}
						if($numberofchildren > 1) {
							my $childnames = join(', ', map { $_->given_names() } @children);
							substr($childnames, rindex($childnames, ', '), 2, ' and ');
							print $childnames;
						}
					}
				}
			}
			$end_of_sentence = 0;
		} elsif($numberofchildren) {
			print "  $pronoun had ",
				($numberofchildren == 1) ? '1 child, ' : "$numberofchildren children, ";
			if($numberofchildren == 1) {
				print $children[0]->given_names();
			} elsif($numberofchildren > 1) {
				my $childnames = join(', ', map { $_->given_names() } @children);
				substr($childnames, rindex($childnames, ', '), 2, ' and ');
				print $childnames;
			}
			$end_of_sentence = 0;
		}

		my $printed_residence;
		my %residencecitations;
		if(scalar(@residences)) {
			# FIXME, handle duplicate locations better
			# TODO: sort in calendar order
			my @residencelist;

			# This is an attempt to handle
			# https://github.com/pjcj/Gedcom.pm/issues/13
			foreach my $residence(@residences) {
				if(ref($residence) eq 'Gedcom::Record') {
					my $place = $residence->place();
					if(defined($place)) {
						if(((!$placeofbirth) || ($place ne $placeofbirth)) &&
						   ((!$placeofmarriage) || ($place ne $placeofmarriage))) {
							push @residencelist, $residence;
							my $src = $residence->source();
							if($src) {
								$src = $ged->get_source($src) unless ref($src);
								if($src eq $citations{$birthcitation}) {
									$residencecitations{$residence} = $birthcitation;
								} else {
									my $r = ++$citationcount;
									$residencecitations{$residence} = $r;
									$citations{$r} = $src;
								}
							}
						}
					} else {
						red_warning({ person => $person, warning => 'Residence record contains no location' });
					}
				}
			}

			my $count = 0;
			foreach my $residence(@residencelist) {
				if($count == 0) {
					print '.' unless($end_of_sentence);
					print '  During ',
						lc($person->possessive()),
						' life ', lcfirst($pronoun), ' lived';
				} elsif($residencelist[$count - 1]->place() eq $residence->place()) {
					$count++;
					next;
				}
				print place({ person => $person, record => $residence });
				my $date = $residence->date();
				if($date) {
					print ' (',
						year({ person => $person, date => $date }),
						')';
				}
				if($residencecitations{$residence}) {
					print '[', $residencecitations{$residence}, ']';
				}
				if($residence->place()) {
					if(($count <= (scalar(@residencelist) - 1)) && ($residencecitations{$residence})) {
						my $peek = $residencelist[$count + 1];
						if($peek && $peek->place() && ($peek->place() eq $residence->place())) {
							if($residencecitations{$peek}) {
								print '[', $residencecitations{$peek}, ']';
							}
						}
					}
				}
				$count++;
				if($count < (scalar(@residencelist) - 1)) {
					print ';';
				} elsif($count == (scalar(@residencelist) - 1)) {
					print '; and';
				}
				$printed_residence = 1;
			}
			if($printed_residence) {
				print '.';
				$end_of_sentence = 1;
			}
		}

		if($placeofburial || $dateofburial) {
			if($aod || ((!$dateofdeath) && !$placeofdeath)) {
				print '.' if((!$end_of_sentence) && (scalar(@spouses) || $numberofchildren));
				if($dateofburial) {
					print "  $pronoun was buried ",
						year({ person => $person, date => $dateofburial });
					if($placeofburial) {
						if($placeofbirth && ($placeofburial eq $placeofbirth)) {
							print ' in ',
								lcfirst($person->possessive()),
								' home town of ';
							my $city = $placeofburial;
							if($city =~ /^(.+?),/) {
								print $1;
							} else {
								print place({ person => $person, place => $placeofburial });
							}
						} else {
							print place({ person => $person, place => $placeofburial, there => $placeofdeath, places_printed => \%places_printed, must_postdate => $birth_dt });
						}
					}
				} elsif($placeofbirth && ($placeofburial eq $placeofbirth)) {
					print "  $pronoun was buried in ",
						lcfirst($person->possessive()),
						' home town of ';
					my $city = $placeofburial;
					if($city =~ /^(.+?),/) {
						print $1;
					} else {
						print place({ person => $person, place => $placeofburial });
					}
				} else {
					my $p = place({ person => $person, place => $placeofburial, places_printed => \%places_printed, there => $placeofmarriage });
					print "  $pronoun is buried$p";
				}
				$end_of_sentence = 0;
			} elsif($dateofdeath || $placeofdeath) {
				print '.' if(!$end_of_sentence);
				print "  $pronoun died";
				if($dateofdeath) {
					print ' ', year({ person => $person, date => $dateofdeath, must_postdate => $birth_dt });
					if($placeofdeath) {
						print place({ person => $person, place => $placeofdeath });
					}
				} else {
					print place({ person => $person, place => $placeofdeath });
				}
				if($dateofburial) {
					print ' and was buried';
					if($placeofburial) {
						print place({ person => $person, place => $placeofburial, there => $placeofdeath, places_printed => \%places_printed, must_postdate => $birth_dt });
					}
					print ' ', year({ person => $person, date => $dateofburial });
				} else {
					print ' and is buried',
						place({ person => $person, place => $placeofburial, there => $placeofdeath });
				}
				$end_of_sentence = 0;
			}
		} elsif((!$aod) && ($placeofdeath || $dateofdeath)) {
			if($placeofdeath && $placeofmarriage && ($placeofdeath eq $placeofmarriage) && !$printed_residence) {
				print ', which is also where ', lcfirst($pronoun);
			} elsif($aob && scalar(@spouses) && ($numberofchildren == 0)) {
				if($end_of_sentence) {
					print "  $pronoun";
				} else {
					print ' and';
				}
			} else {
				print '.' if(!$end_of_sentence);
				print "  $pronoun";
			}
			print ' died';
			if($dateofdeath) {
				if($placeofbirth && $placeofdeath && ($placeofdeath eq $placeofbirth) && ($placeofdeath =~ /^(.+?),.*/) && !$printed_residence) {
					print " in $1 ",
						year({ person => $person, date => $dateofdeath, must_postdate => $birth_dt });
				} else {
					print ' ', year({ person => $person, date => $dateofdeath, must_postdate => $birth_dt });
					if($placeofdeath && ((!$placeofmarriage) || ($placeofdeath ne $placeofmarriage) || $printed_residence)) {
						print place({ person => $person, place => $placeofdeath });
					}
				}
				print "[$deathcitation]" if($deathcitation);
			} elsif((!$placeofmarriage) || ($placeofdeath ne $placeofmarriage)) {
				print place({ person => $person, place => $placeofdeath });
				print "[$deathcitation]" if($deathcitation);
			}
			$end_of_sentence = 0;
		}
		print '.' if(!$end_of_sentence);
		print "\n";

		foreach my $child(@children) {
			my $dob = get_value({ person => $child, value => 'birth date' });
			if($dob) {
				print "\t", $child->given_names(),
					' was born ',
					year({ person => $child, date => $dob });
				if($opts{'w'} && ($birth_dt || $death_dt) && ($dob =~ /^\d/)) {
					my $d;
					eval {
						$d = $date_parser->parse_date_value(date => $dob);
					};
					if($d) {
						$d = $d->{one_date};
						if($birth_dt && ($d <= $birth_dt)) {
							if($opts{'f'}) {
								die $child->name(), ': Born before parent was born';
							}
							red_warning({ person => $child, warning => 'born before parent was born' });
						}
						if($sex eq 'F') {
							if($death_dt && ($d > $death_dt)) {
								if($opts{'f'}) {
									die $child->name(), ': Born after mother died';
								}
								red_warning({ person => $child, warning => 'born after mother died' });
							}
						} elsif($death_dt && ($d > ($death_dt + $ninemonths))) {
							# Allow the child to be
							# born up to 9 months
							# after the death of the
							# father
							if($opts{'f'}) {
								die $child->name(), ': Born more than 9 months after father died';
							}
							red_warning({ person => $child, warning => 'born more than 9 months after father died' });
						}
					}
				}
				print ".\n";
			} elsif(get_value({ person => $child, value => 'baptism date' })) {
				print "\t", $child->given_names(),
					' was born c',
					get_value({ person => $child, value => 'baptism date' }),
					".\n";
			}
		}
		if($opts{'c'}) {
			foreach my $citation(1..$citationcount) {
				print "[$citation]: ", $citations{$citation}->title(), "\n";
				# TODO - print NOTE
			}
		}
		print '-' x 80, "\n";
	} else {
		my ($dob, $dod);
		eval {
			$dob = $person->get_value('birth date');
			$dod = $person->get_value('death date');
		};
		if($@) {
			warn "Can't parse record for $firstname $lastname";
			return;
		}

		if(defined($dob) && (!$onlydeaths) && !$onlybirthdays) {
			my $d;
			if(($dob !~ /^\d{4}$/) && ($dob =~ /^\d/)) {
				eval {
					$d = $date_parser->parse_date_value(date => $dob);
				};
			}
			if($d) {
				$birth_dt = $d->{one_date};
				print "\tBorn: ", $birth_dt->strftime('%x');
			} else {
				if($@) {
					if($opts{'f'}) {
						die $@;
					}
					if($opts{'w'}) {
						red_warning({ person => $person, warning => $@ });
					}
				}
				print "\tBorn: $dob";
			}
			print "\n";
		}
		if(defined($dod) && (!$onlydeaths) && !$onlybirthdays) {
			my $d;
			if(($dod !~ /^\d{4}$/) && ($dod =~ /^\d/)) {
				eval {
					$d = $date_parser->parse_date_value(date => $dod);
				};
			}
			if($d) {
				$death_dt = $d->{one_date};
				print "\tDied: ", $death_dt->strftime('%x');
			} else {
				if($@) {
					if($opts{'f'}) {
						die $@;
					}
					if($opts{'w'}) {
						red_warning({ person => $person, warning => $@ });
					}
				}
				print "\tDied: $dod\n";
			}
			print "\n";
		}
	}
	$printed{$person->{'xref'}} = 1;
}

sub Gedcom::Individual::as_string
{
	my $self = shift;
	my %args = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $name = $self->name();

	$name =~ s/\///g;
	$nameparser->parse($name);

	my %name_components = $nameparser->components();
	my $first_name = $name_components{'given_name_1'};
	my $middle_name = $name_components{'middle_name'};
	my $surname = $name_components{'surname_1'} || $self->surname();
	my $suffix = $name_components{'suffix'};

	my $has_maiden_name;
	if($args{'nee'}) {
		my $sex = get_value({ person => $self, value => 'sex' });
		if(defined($sex) && ($sex eq 'F')) {
			my $husband = $self->husband();
			if(defined($husband)) {
				$surname = $husband->surname();
				$has_maiden_name = 1;
			}
		}
	}

	my $rc;
	
	if($args{'title'}) {
		$rc = $name_components{'title_1'};
		if($rc) {
			$rc .= ' ';
		}
	}

	if($first_name) {
		$rc .= $first_name;
	} else {
		$rc .= $self->given_names();
	}

	if($args{'middle_names'} && $middle_name) {
		$rc .= " $middle_name";
	}

	if($surname) {
		if($rc) {
			$rc .= ' ';
		}
		$rc .= normalize_name($surname);
		if($suffix) {
			$rc .= " $suffix";
		}
	}

	if($has_maiden_name && $self->surname()) {
		$rc .= ' (nee ' . normalize_name($self->surname()) . ')';
	}

	if(!defined($rc)) {
		if($opts{'f'}) {
			die "Can't determine name of this person";
		}
		if($opts{'w'}) {
			red_warning({ warning => "Can't determine name of this person" });
			$args{'include_years'} = 1;
			$rc = 'Unknown person ';
		}
	}
	if($args{'include_years'}) {
		my $dob = get_value({ person => $self, value => 'birth date' });

		my $yob;	# Year of birth
		if($dob && ($dob =~ /.*?(\d{3,4})/)) {
			if($dob =~ /^(Abt|ca?)\.?\s*(.+)/i) {
				$yob = "c$2";
			} else {
				$yob = $1;
			}
		} else {
			$dob = get_value({ person => $self, value => 'baptism date' });
			if($dob && ($dob =~ /.*?(\d{3,4})/)) {
				$yob = "c$1";
			}
		}

		my $dod = get_value({ person => $self, value => 'death date' });

		my $yod;	# Year of death
		if($dod && ($dod =~ /.*?(\d{3,4})/)) {
			$yod = $1;
		} else {
			$dod = get_value({ person => $self, value => 'burial date' });
			if($dod && ($dod =~ /.*?(\d{3,4})/)) {
				$yod = "c$1";
			}
		}

		$rc .= ' ' if($yob || $yod);

		if($yob) {
			$rc .= $yob;
		}

		$rc .= ' -' if($yob || $yod);

		if($yod) {
			return "$rc $yod";
		}
	}

	return $rc;
}

sub normalize_name
{
	# my $name = shift;

	# my $rc;
	# foreach my $word(split(/-| |'/, lc($name))) {
		# $rc .= '-' if($rc && ($name =~ /-/));
		# $rc .= "'" if($rc && ($name =~ /'/));
		# $rc .= ' ' if($rc && ($name =~ / /));
		# $rc .= ucfirst($word);
	# }

	# return $rc;

	return Lingua::EN::NameCase::nc(shift);
}

sub Gedcom::Individual::pronoun
{
	my $self = shift;

	my $sex = get_value({ person => $self, value => 'sex' });

	if(defined($sex)) {
		if($sex eq 'F') {
			return 'She';
		}
		if($sex eq 'M') {
			return 'He';
		}
	}
	return 'They';
}

sub Gedcom::Individual::possessive
{
	my $self = shift;

	my $rc = {
		'He' => 'His',
		'She' => 'Her',
		'They' => 'Their',
	};

	return $rc->{$self->pronoun()};
}

# FIXME: currently only finds ancestors
# TODO: find in-laws
# See http://www.myrelative.com/html/relationship.html for inspiration
sub Gedcom::Individual::relationship
{
	my $self = shift;
	my $other = shift;

	unless(@myancestors) {
		@myancestors = $self->ancestors();
	}
	return unless @myancestors;

	my $sex = get_value({ person => $other, value => 'sex' });
	if((!defined($sex)) || ($sex !~ /[MF]/)) {
		if($opts{'f'}) {
			die "\n", $other->as_string(), ": unknown sex\n";
		}
		if($opts{'w'}) {
			red_warning({ person => $other, warning => 'unknown sex' });
		}
		return;
	}

	foreach my $person1(@myancestors) {
		if($person1 eq $other) {
			# Direct ancestor
			my $steps = stepsabove($self, $other, 0);
			my $title = ($sex eq 'M') ? 'father' : 'mother';
			if($steps >= 5) {
				$steps -= 2;
				return "$steps times great-grand$title";
			} elsif($steps == 1) {
				return $title;
			} elsif($steps == 2) {
				return "grand$title";
			} elsif($steps == 3) {
				return "great-grand$title";
			} elsif($steps == 4) {
				return "great-great-grand$title";
			} elsif($steps <= 0) {
				if($opts{'f'}) {
					die $other->as_string(), ": BUG - not a direct ancestor, steps = $steps";
				} else {
					warn colored($other->as_string() . ": BUG - not a direct ancestor, steps = $steps", 'red');
				}
			}
		}
	}

	my @ancestors2 = $other->ancestors();
	return unless(@ancestors2);

	foreach my $person1(@myancestors) {
		foreach my $person2(@ancestors2) {
			# print $person1->as_string(), '->', $person2->as_string(), "\n";
			# G::C is noisy
			# my $c = Gedcom::Comparison->new($person1, $person2);
			# if($c->identical($person2)) {
				# die 'match found';
			# }
			if($person1 eq $person2) {
				# Common ancestor is $person2
				my $steps1 = stepsabove($self, $person1, 0);
				return if($steps1 > 7);
				my $steps2 = stepsabove($other, $person2, 0);
				return if($steps2 > 7);

				# It would be nice to do this as an algorithm, but this will do
				# e.g. 2, 1 is uncle
				my %male_relationships = (
					1 << 8 | 1 => 'brother',
					1 << 8 | 2 => 'nephew',
					2 << 8 | 1 => 'uncle',
					3 << 8 | 1 => 'great-uncle',
					4 << 8 | 1 => 'great-great-uncle',
					2 << 2 | 2 => 'cousin',
					2 << 8 | 3 => 'first cousin once-removed',
					3 << 8 | 2 => 'first cousin once-removed',
					2 << 8 | 4 => 'first cousin twice-removed',
					4 << 8 | 2 => 'first cousin twice-removed',
					3 << 8 | 3 => 'second cousin',
					3 << 8 | 4 => 'second cousin once-removed',
					6 << 8 | 5 => 'fourth cousin once-removed',
					6 << 8 | 4 => 'third cousin twice-removed',
				);
				my %female_relationships = (
					1 << 8 | 1 => 'sister',
					1 << 8 | 2 => 'niece',
					2 << 8 | 1 => 'aunt',
					3 << 8 | 1 => 'great-aunt',
					4 << 8 | 1 => 'great-great-aunt',
					2 << 2 | 2 => 'cousin',
					2 << 8 | 3 => 'first cousin once-removed',
					3 << 8 | 2 => 'first cousin once-removed',
					2 << 8 | 4 => 'first cousin twice-removed',
					4 << 8 | 2 => 'first cousin twice-removed',
					3 << 8 | 3 => 'second cousin',
					3 << 8 | 4 => 'second cousin once-removed',
					6 << 8 | 5 => 'fourth cousin once-removed',
					6 << 8 | 4 => 'third cousin twice-removed',
				);

				my $rc = ($sex eq 'M') ?
					$male_relationships{($steps1 << 8) | $steps2} :
					$female_relationships{($steps1 << 8) | $steps2};
				# print "$steps1, $steps2\n" unless($rc);
				return $rc;
			}
		}
	}
}

sub stepsabove
{
	my $person = shift;
	my $target = shift;
	my $count = shift;

	return -1 if($count == -1);

	if($person eq $target) {
		return $count;
	}
	my @father = $person->father();
	my $father = $father[0];
	my @mother = $person->mother();
	my $mother = $mother[0];

	if($father) {
		my $rc = stepsabove($father, $target, $count + 1);
		return $rc if($rc != -1);
	}
	if($mother) {
		return stepsabove($mother, $target, $count + 1);
	}
	return -1;
}

sub year
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $string = $params{'string'} || $params{'date'};

	if(!defined($string)) {
		$string = $params{'record'};
		return unless($string);

		$string = $string->date();
		return unless($string);
	}

	if($string =~ /^\d{4}$/) {
		return "in $string";
	}
	if($string =~ /^(Abt|ca?)\.?\s*(.+)/i) {
		return "c$2";
	}
	if($string =~ /^\d/) {
		my $person = $params{'person'};
		my $d;
		eval {
			$d = $date_parser->parse_date_value(date => $string);
		};
		if($d) {
			$d = $d->{one_date};
			my $must_postdate = $params{'must_postdate'};
			if($must_postdate) {
				if($opts{'f'}) {
					die $person->as_string(), ": Something is wrong with the date $string which should be after ", $must_postdate->strftime('%x') if($d < $must_postdate);
				}
				if($opts{'w'}) {
					red_warning({ person => $person, warning => "something is wrong with the date $string which should be after " . $must_postdate->strftime('%x') }) if($d < $must_postdate);
				}
			}
			my $must_predate = $params{'must_predate'};
			if($must_predate) {
				if($opts{'f'}) {
					die $person->as_string(), ": Something is wrong with the date $string which should be before ", $must_predate->strftime('%x') if($d > $must_predate);
				}
				if($opts{'w'}) {
					red_warning({ person => $person, warning => "something is wrong with the date $string which should be before " . $must_predate->strftime('%x') }) if($d > $must_predate);
				}
			}
			return 'on ', $d->strftime('%x');
		}
		if($@) {
			# RT 107354
			chomp $@;
			if($opts{'f'}) {
				if($person) {
					die $person->as_string() . ": $@";
				} else {
					die $@;
				}
			}
			if($opts{'w'}) {
				red_warning({ person => $person, warning => $@ });
			}
		}
		return "on $string";
	}
	if($string =~ /^bef.? (\d{4})/i) {
		return "before $1";
	}
	if($string =~ /^by.? (\d{4})/i) {
		return "by $1";
	}
	if($string =~ /^By (\d{4})/i) {
		my $person = $params{'person'};
		if($opts{'f'}) {
			if($person) {
				die $person->as_string() . " says 'By' instead of 'Bef'";
			} else {
				die "Got 'By' instead of 'Bef'";
			}
		}
		if($opts{'w'}) {
			if($person) {
				red_warning({ person => $person, warning => "says 'By' instead of 'Bef'" });
			} else {
				warn colored("Got 'By' instead of 'Bef'", 'red');
			}
		}
		return "before $1";
	}

	return "in $string";
}

sub place
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $place = $params{'place'};
	if(!defined($place)) {
		$place = $params{'record'};
		return unless($place);

		$place = $place->place();
		return unless($place);
	}

	my $there = $params{'there'};

	if($there && ($place eq $there)) {
		return ' there';
	}

	validate_place({ person => $params{'person'}, place => $place });

	if($place =~ /^\d/) {
		return " at $place";
	}
	if($place eq 'USA') {
		return ' in the USA';
	}
	$place = " in $place";

	# Only print the town if the location has already been printed
	my $places_printed = $params{'places_printed'};
	if($places_printed && $places_printed->{$place} && $place =~ /^(.+?),/) {
		return $1;
	}
	return $place;
}

sub validate_place
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $place = $params{'place'};
	die 'place is not optional' unless $place;

	if($places{$place}) {
		return $places{$place};
	}

	# US and Canada are more logical, using city,county,state,country, than other countries
	if($opts{'w'} && (($place !~ /,.*,.*,.*/) || ($place =~ /USA|Canada/i))) {
		if($place =~ /(.*),\s*Independent Cities\s*(.*)/i) {
			$place = "$1$2";
		}
		if(!$geocoder->geocode($place)) {
			# Don't die because there are many
			# false positives as locations
			# may no longer exist
			my $person = $params{'person'};
			if($person) {
				warn colored($person->as_string({ include_dates => 1 }) . ": unknown location $place", 'red');
			} else {
				warn colored("Unknown location $place", 'red');
			}
			$places{$place} = 0;
			return 0;
		}
	}
	$places{$place} = 1;
	return 1;
}

sub print_sibling_baptism
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $person = $params{'person'};
	my $siblings = $params{'siblings'};
	my $date = $params{'date'};
	my $birthdate = $params{'birthdate'};

	my @onsameday;
	foreach my $sibling(@{$siblings}) {
		my $dateofsiblingbaptism = get_value({ person => $sibling, value => 'baptism date' });
		if($dateofsiblingbaptism && ($dateofsiblingbaptism eq $date)) {
			push @onsameday, $sibling;
		}
	}
	my $count = 0;
	foreach my $sibling(@onsameday) {
		if($count == 0) {
			print ', the same day as ';
		} elsif($count < (scalar(@onsameday) - 1)) {
			print ', ';
		} elsif($count == (scalar(@onsameday) - 1)) {
			print ' and ';
		}
		print lcfirst($person->possessive()), ' ';
		my $dateofsiblingbirth = get_value({ person => $sibling, value => 'birth date' });
		if($dateofsiblingbirth && $birthdate && ($dateofsiblingbirth eq $birthdate)) {
			print 'twin ';
		}
		print $sibling->sex() eq 'M' ? 'brother ' : 'sister ',
			$sibling->given_names();
		$count++;
	}
}

sub red_warning
{
	if($opts{'w'}) {
		my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

		if($params{'person'}) {
			warn colored($params{'person'}->as_string() . ': ' . $params{'warning'}, 'red');
		} else {
			warn colored($params{'warning'}, 'red');
		}
	}
}

# Cache gedcom values
sub get_value
{
	my %params = (ref($_[0]) eq 'HASH') ? %{$_[0]} : @_;

	my $person = $params{'person'};
	my $value = $params{'value'};

	my $xref = $person->{'xref'};
	tie (my %values, 'Tie::Static');

	if(defined($values{$xref}{$value})) {
		return $values{$xref}{$value};
	}

	my $rc = $person->get_value($value);
	$values{$xref}{$value} = $rc;
	return $rc;
}
